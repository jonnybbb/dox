allprojects {

    apply plugin: 'idea'

    group = 'ch.silviowangler.dox'
    version = '0.1-SNAPSHOT'
    project.ext.springVersion = '3.2.0.M1'
    project.ext.springDataVersion = '1.1.0.RELEASE'
    project.ext.hibernateVersion = '4.1.4.Final'
    project.ext.slf4jVersion = '1.6.4'
    project.ext.logbackVersion = '1.0.3'
    project.ext.junitVersion = '4.10'
    project.ext.tilesVersion = '2.2.2'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging'
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://repository.jboss.org/nexus/content/groups/public'
            url 'http://repo.springsource.org/libs-milestone'
            url 'http://repo.springsource.org/libs-snapshot'
        }
    }

    dependencies {
        compile "org.slf4j:slf4j-api:${ext.slf4jVersion}"
        testCompile "junit:junit:${ext.junitVersion}", 'org.mockito:mockito-all:1.9.0'
    }

    jar {
        manifest {
            attributes('Implementation-Title': "DOX : ${project.name}", 'Implementation-Version': project.version)
        }
    }
}

project(':core') {
    dependencies {
        compile project(':api'), project(':domain'),
                "org.springframework:spring-orm:${ext.springVersion}",
                'joda-time:joda-time:2.1', 'org.hibernate:hibernate-validator:4.3.0.Final'
        runtime "org.slf4j:jcl-over-slf4j:${ext.slf4jVersion}"
    }
}

project(':domain') {
    dependencies {
        compile "org.hibernate:hibernate-core:${ext.hibernateVersion}", "org.hibernate:hibernate-entitymanager:${ext.hibernateVersion}"
        compile("org.springframework.data:spring-data-jpa:${ext.springDataVersion}") {
            exclude group: 'org.slf4j', module: 'jcl-over-slf4j'
        }
    }
}

project(':functional-tests') {
    dependencies {
        testCompile project(':core'), "org.springframework:spring-test:${ext.springVersion}"
        runtime "ch.qos.logback:logback-classic:${ext.logbackVersion}", 'com.h2database:h2:1.3.166'
    }
}