allprojects {

    apply plugin: 'idea'

    group = 'ch.silviowangler.dox'
    version = '0.1-SNAPSHOT'
    project.ext.springVersion = '3.2.0.M1'
    project.ext.springDataVersion = '1.1.0.RELEASE'
    project.ext.hibernateVersion = '4.1.5.Final'
    project.ext.slf4jVersion = '1.6.4'
    project.ext.logbackVersion = '1.0.3'
    project.ext.junitVersion = '4.10'
    project.ext.tilesVersion = '2.2.2'
    project.ext.commonsIOVersion = '2.3'
    project.ext.jodaTimeVersion = '2.1'
    project.ext.h2Version = '1.3.167'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging'
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://repository.jboss.org/nexus/content/groups/public'
            url 'http://repo.springsource.org/libs-milestone'
            url 'http://repo.springsource.org/libs-snapshot'
        }
    }

    dependencies {
        compile "org.slf4j:slf4j-api:${ext.slf4jVersion}"
        testCompile "junit:junit:${ext.junitVersion}", 'org.mockito:mockito-all:1.9.0'
    }

    jar {
        manifest {
            attributes('Implementation-Title': "DOX : ${project.name}", 'Implementation-Version': project.version)
        }
    }
}

project(':core') {
    dependencies {
        compile project(':api'), project(':domain'),
                "org.springframework:spring-orm:${ext.springVersion}",
                'commons-codec:commons-codec:1.6', 'com.itextpdf:itextpdf:5.3.0',
                'commons-beanutils:commons-beanutils:1.8.3', "commons-io:commons-io:${ext.commonsIOVersion}",
                "joda-time:joda-time:${ext.jodaTimeVersion}"
        runtime "org.slf4j:jcl-over-slf4j:${ext.slf4jVersion}", "ch.qos.logback:logback-classic:${ext.logbackVersion}"
    }
}

project(':domain') {
    dependencies {
        compile project(':api'), "org.hibernate:hibernate-core:${ext.hibernateVersion}", "org.hibernate:hibernate-entitymanager:${ext.hibernateVersion}"
        compile("org.springframework.data:spring-data-jpa:${ext.springDataVersion}") {
            exclude group: 'org.slf4j', module: 'jcl-over-slf4j'
        }
        compile "joda-time:joda-time:${ext.jodaTimeVersion}"
    }
}

project(':functional-tests') {
    dependencies {
        testCompile project(':core'), "org.springframework:spring-test:${ext.springVersion}", "commons-io:commons-io:${ext.commonsIOVersion}"
        runtime "ch.qos.logback:logback-classic:${ext.logbackVersion}", "com.h2database:h2:${ext.h2Version}"
    }
}

project(':web') {
    apply plugin: 'war'

    war.archiveName = "dox-${version}.war"

    dependencies {
        compile project(':core')
        compile "org.springframework:spring-webmvc:${ext.springVersion}"
        providedRuntime 'javax.servlet:javax.servlet-api:3.0.1'
        runtime "org.apache.tiles:tiles-core:${ext.tilesVersion}"
        runtime "org.apache.tiles:tiles-servlet:${ext.tilesVersion}"
        runtime "org.apache.tiles:tiles-jsp:${ext.tilesVersion}"
        runtime "org.apache.tiles:tiles-template:${ext.tilesVersion}"
        runtime "ch.qos.logback:logback-access:${ext.logbackVersion}"
        runtime 'jstl:jstl:1.2', "com.h2database:h2:${ext.h2Version}", 'jstl:jstl:1.2'

    }
}